group 'com.oldpeng.thymeleaf3-shiro'
version '0.0.1'

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'com.jfrog.bintray'

buildscript {
    ext {
        shiroVersion = '1.3.0'
        slf4jVersion = '1.7.13'
        logbackVersion = '1.1.3'
        thymeleafVersion = '3.0.1.RELEASE'
    }
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.6'
    }
}

sourceCompatibility = 1.7

repositories {
    jcenter()
}

dependencies {
    compile("org.apache.shiro:shiro-core:$shiroVersion")
    compile("org.thymeleaf:thymeleaf:${thymeleafVersion}")
    compile("org.slf4j:slf4j-api:$slf4jVersion")
    compile("org.slf4j:jcl-over-slf4j:$slf4jVersion")
    compile("org.slf4j:jul-to-slf4j:$slf4jVersion")
    compile("org.slf4j:log4j-over-slf4j:$slf4jVersion")
    compile("ch.qos.logback:logback-classic:$logbackVersion")
    compile("ch.qos.logback:logback-core:$logbackVersion")
    compile("commons-lang:commons-lang:2.6")

    compile group: 'com.alibaba', name: 'fastjson', version: '1.2.15'

    testCompile("org.easymock:easymock:3.1")
    testCompile group: 'junit', name: 'junit', version: '4.11'
}

/**************************************************************/
/**************************************************************/
/********************* publish jar ****************************/
/**************************************************************/
/**************************************************************/


task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

def siteUrl = "https://github.com/usydapeng/thymeleaf3-shiro"
def gitUrl = "git@github.com:usydapeng/thymeleaf3-shiro.git"


task writeNewPom << {
    pom {
        project {
            packaging 'jar'
            // Add your description here
            name 'thymeleaf3 shiro sdk' 	//项目描述
            url siteUrl
            inceptionYear '2016'
            // Set your license
            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                }
            }
            developers {
                developer {
                    id 'usydapeng'		//填写的一些基本信息
                    name 'dapeng'
                    email 'usydapeng@gmail.com'
                }
            }
            scm {
                connection gitUrl
                developerConnection gitUrl
                url siteUrl
            }
        }
    }.writeTo("$buildDir/pom.xml")
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")
    configurations = ['archives']
    pkg {
        repo = "maven"
        name = "thymeleaf3-shiro"	//发布到JCenter上的项目名字
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["Apache-2.0"]
        publish = true
    }
}
